use std.convert.to_string;

struct Result[T, E] {
    var successful: bool;

    @unmanaged
    var value: T;

    @unmanaged
    var error: E;

    pub func new_success(value: T) -> Result[T, E] {
        return Result[T, E] {
            successful: true,
            value,
            error: undefined,
        };
    }

    pub func new_failure(error: E) -> Result[T, E] {
        return Result[T, E] {
            successful: false,
            value: undefined,
            error,
        };
    }

    pub func unwrap(@[byval, unmanaged] self) -> T {
        if !self.successful {
            panic("tried to unwrap error result: " + to_string(self.error));
        }

        return self.value;
    }

    pub func unwrap_error(@[byval, unmanaged] self) -> E {
        if self.successful {
            panic("tried to unwrap successful result to error");
        }

        return self.error;
    }

    pub func __str__(self) -> String {
        if self.successful {
            return to_string(self.value);
        } else {
            return to_string(self.error);
        }
    }

    meta if meta(T).is_resource || meta(E).is_resource {
        pub func __deinit__(self) {
            if self.successful {
                __builtin_deinit(self.value);
            } else {
                __builtin_deinit(self.error);
            }
        }
    }
}
