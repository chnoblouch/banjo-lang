use std.{system.panic, convert.to_string};

struct Result[T, E] {
    var successful: bool;
    var value: T;
    var error: E;
    
    pub func new_success(value: T) -> Result[T, E] {
        return Result[T, E] {
            successful: true,
            value,
            error: undefined
        };
    }
    
    pub func new_failure(error: E) -> Result[T, E] {
        return Result[T, E] {
            successful: false,
            value: undefined,
            error
        };
    }
    
    pub func unwrap(self) -> T {
        if !self.successful {
            panic(to_string(self.error));
        }
        
        self.successful = false;
        return self.value;
    }
    
    pub func __str__(self) -> String {
        if self.successful {
            return to_string(self.value);
        } else {
            return to_string(self.error);
        }
    }
    
    pub func __deinit__(self) {
        meta if meta(T).has_method("__deinit__") {
            if self.successful {
                self.value.__deinit__();
            }
        }
        
        meta if meta(E).has_method("__deinit__") {
            if !self.successful {
                self.error.__deinit__();
            }
        }
    }
}